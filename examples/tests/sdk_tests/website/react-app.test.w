bring util;
bring ex;
bring cloud;
bring http;

let api = new cloud.Api();

api.get("/", inflight () => {
  return {body: "hi!", status: 200};
});

// the project isn't actually react (since it can make wing's size much larger) 
// but a mock with start and build commands
let website = new ex.ReactApp(
  projectPath: "./react-website", 
  buildDir: "/build/public",
  useBuildCommand: true // builds the website
);

let preflightVariable = "preflight variable";
website.addEnvironment("apiUrl", api.url);
website.addEnvironment("anotherEnvVar", preflightVariable);

test "website is working" {
  assert(http.get(website.url).ok);
  let wingEnv = http.get(website.url + "/wing.js").body ?? "";
  let expected = "// This file is generated by wing\nwindow.wingEnv = {\n  \"apiUrl\": \"${api.url}\",\n  \"anotherEnvVar\": \"${preflightVariable}\"\n};";
  assert(wingEnv == expected);
}

